5 REM SAVE "FORTKNOX.BAS
10 REM FORT KNOX
20 REM ****************
30 REM (C)2025 GameCast
40 REM ****************
50 REM VERSION 2.0
60 REM ****************
100 REM set list defusr
110 READ N,X$,T$:IF N=255 THEN GOTO130
111 W=&HF39A+N*2:POKE W,VAL("&h"+RIGHT$(X$,2)):POKEW+1,VAL("&h"+LEFT$(X$,2)):GOTO 110
112 DATA 1,"DA00",SFX
113 DATA 2,"0090",RESET PSG
114 DATA 3,"D000",MAIN LOOP
115 DATA 4,"D003",RESET ALL
129 DATA 255,"",""
130 DEFUSR5=&HDE00:REM CTRLSPR
140 CLEAR500,&HBFFF
150 DEFINTA-Y:DEFSTRZ:DIMZ(28)
160 HI=512:VDP(1)=98
170 REM INIT INTRO MUSIC!
180 U$="T150"
190 V1$="M5000S1V12R1R8R8R4R4L8O5BAL4GL8GAL4GO4L8BO5CDEDO4BO5L4DL8GAL4BBL8BAGAL4BAAL8BAL4GL8GAL4GO4L8BO5CDEDO4BO5L4DL8GAL4BBBL8GAL4BAL2G"
200 V2$="V10O4R4.L8DD-DD-DR4.DD-DD-DR4.DD-DD-DR4.DD-DD-DR4.DD-DD-DR4.R8R8R8L8D-DR4.DD-DD-DR4.DD-DD-DR4.DD-DD-DR1"
210 V3$="V10L2O2GO3DO2GO3DO2GO3DO2GO3DO2GO3DO2DAGO3DO2GO3DO2GO3DL4O2DF#GR4"
225 REM INTRO
230 CLS
240 FOR A=0 TO31:PUT SPRITE A,,,0:NEXTA
250 PRINT" SCORE        HI SCORE"
260 PRINTSC*100;SPC(10);HI*100
270 LOCATE0,8
280 LOCATE11,5:PRINTCHR$(200)+CHR$(202):LOCATE11,6:PRINTCHR$(201)+CHR$(203)
290 LOCATE13,5:PRINTCHR$(204)+CHR$(206):LOCATE13,6:PRINTCHR$(205)+CHR$(207)
300 LOCATE16,5:PRINTCHR$(208)+CHR$(210):LOCATE16,6:PRINTCHR$(209)+CHR$(211)
310 LOCATE18,5:PRINTCHR$(212)+CHR$(214):LOCATE18,6:PRINTCHR$(213)+CHR$(215)
320 LOCATE6,8:PRINT"the bagman return"
330 LOCATE 1,12:PRINT"1UP BONUS EVERY FOR 30000 PTS"
340 LOCATE 10,14:PRINT"INTRO MUSIC BY"
350 LOCATE 2,16:PRINT"CLAUDIO LUPO & DANIEL TKACH"
360 LOCATE 0,18:PRINT"   @ 1982 VALADON AUTOMATION"
370 LOCATE 2,20:PRINT"PUSH SPACE OR FIRE TO START"
380 LOCATE 0,22:PRINT" @ 2025 GAMECAST & danysoft"
410 IF STRIG(0)=-1 THENPOKE&HDA03,5:A=USR1(0):GOTO660
420 IF STRIG(1)=-1 THENPOKE&HDA03,5:A=USR1(0):GOTO660
430 TI=TI+1:IFTI>1500THENTI=0:GOTO450
440 GOTO 410
450 CLS:SP=1:X=16:Y=32:XC=16:YC=64:CN=31
460 PUTSPRITE6,(X,Y),6,SP+K:PUTSPRITE7,(X,Y),10,SP+1+K:PUTSPRITE8,(X,Y),15,SP+2+K
470 PUTSPRITE2,(XC,YC),15,CN+M:PUTSPRITE3,(XC,YC),6,CN+1+M:PUTSPRITE4,(XC+16,YC),15,CN+2+M:PUTSPRITE5,(XC+16,YC),6,CN+3+M
480 LOCATE2,12:PRINTCHR$(144)+CHR$(145)
490 LOCATE2,13:PRINTCHR$(146)+CHR$(147)
500 LOCATE2,16:PRINTCHR$(162)+CHR$(163)
510 LOCATE6,4:PRINT"player1"
520 LOCATE6,8:PRINT"bird condor"
530 LOCATE6,13:PRINT"bag"
540 LOCATE6,16:PRINT"whellbarrow"
550 LOCATE0,0:PRINT" SCORE        HI SCORE"
560 PRINTSC*100;SPC(10);HI*100
570 LOCATE2,20:PRINT"TAKE ALL THE BAGS AND CARRY"
580 LOCATE4,21:PRINT"THEM ON THE WHEELBARROW"
590 PLAY U$,U$,U$
600 PLAY V1$,V2$,V3$
610 IF STRIG(0)=-1 THEN230
620 IF STRIG(1)=-1 THEN230
630 TI=TI+1:IFTI>1500THENTI=0:GOTO230
640 GOTO610
650 REM SCORE,LIVES
660 CLS
670 LI=2:SC=0:BO=300:LV=1:ST=1:SS=0:PG=0
680 REM LOADER LEVELS
690 IF LV=1 THEN BLOAD"sfbank00.vrm",S
700 IF LV=2 THEN BLOAD"sfbank01.vrm",S
710 IF LV>2THENLV=1:GOTO690
720 CLS:FORI=0TO31:PUTSPRITEI,,,0:NEXT:LOCATE 11,10:PRINT"STAGE";ST
730 LOCATE 12,12:PRINT"READY"
732 A=USR2(0)
735 PLAY"V14T180O5L8DC#DEDC#O4BA#BO5CO4BA","V14T180O2F#V13O3F#O4CV15O2GV13O3GO4D#","T180R4O4CDR4O3BA"
737 PLAY"T180S0M9600GBAGF#AEF#GEC#O3AV15O4DL8V14O3D","T180V14O2EV13O3GF#V14GV13O3GV14O2AD","T180R4BAR4BAF#"
740 FORA=0TO6000:NEXT
750 CLS:LOCATE 1,0:PRINT"SCORE":LOCATE12,0:PRINT"HI-SCORE"
760 LOCATE0,1:PRINTSC*100:LOCATE11,1:PRINTHI*100
770 LF=LI:IFLF>6THENLF=6
780 FOR A=1 TO LF
790 LOCATE21,0:PRINTSTRING$(LF,134);
800 NEXT
810 L=PG:L=L+1:C$="00":GOSUB1610:A$=B$:L=LV:GOSUB1610
820 LOCATE 27,0:PRINT "SCENE":LOCATE 27,1:PRINT A$;"-";B$
830 REM INIT VAR
840 X=48:Y=16:SP=1:SA=0:CR=0
850 GA=0
854 FORJ=0TO31:PUTSPRITEJ,(0,0),0,0:NEXT
855 GOSUB 1348:V=2048:DEFUSR=&HD012:A=USR(0):V=2048:REM
860 XA=144-24:YB=16:REM cariola
865 DEFUSR=&HD003:A=USR(0)
866 POKE&HD016,0:REMuovo
867 POKE&HD017,0:REMcondor
868 POKE&HD015,0
870 REM
875 A=USR4(1+PG)
880 A=USR3(0)
885 INTERVALON:ONINTERVAL=200GOSUB1730
900 ON STRIG GOSUB 1210,1210,1210
910 STRIG(0)ON:STRIG(1)ON
920 ONSTICK(0)ORSTICK(1)GOSUB1100,1100,1110,1110,1120,1120,1130,1130
930 IFSTICK(0)ORSTICK(1)>0THENK=K+1ANDK<1
940 X1=X:Y1=Y
955 POKE&HDE03,Y:POKE&HDE04,X:POKE&HDE05,SP+K
956 A=USR5(0)
960 IFP=1THENPUTSPRITE10,(X+16,Y+7),6,21
970 IFX>240THENIFPG=7THENX=X-8ELSEIFPG<7THEN PG=PG+1:A=USR4(1+PG):A=USR3(0):X=8:GOSUB1180:GOSUB1620:POKE&HDA03,8:A=USR1(0):FORI=2TO5:PUTSPRITEI,,0,0:NEXTI:PUTSPRITE9,,0,0:TI=0:CR=0:XC=0:YC=0:XO=0:YO=0
980 IFX<8THENIFPG=0THENX=X+8ELSEIFPG>0THEN PG=PG-1:A=USR4(1+PG):A=USR3(0):X=240:GOSUB1190:GOSUB1620:POKE&HDA03,9:A=USR1(0):FORI=2TO5:PUTSPRITEI,,0,0:PUTSPRITE9,,0,0:NEXTI:TI=0:CR=0:XC=0:YC=0:XO=0:YO=0
990 IFSC=>BOTHENBO=BO+300:GOSUB1680:LI=LI+1:LF=LI:IFLF>6THENFORI=0TO1000:NEXTIELSELOCATE20+LF,0:PRINTCHR$(134):FORI=0TO1000:NEXTI
1000 IFSP=7ANDVPEEK(6144+INT((Y+16)/8)*32+((X+8)/8))=32THENGOSUB1070
1010 IFSP=1ANDVPEEK(6144+INT((Y+16)/8)*32+((X)/8))=32THENGOSUB1070
1020 IFTIME>2000THENTIME=0:CR=1
1030 IFCR=1THENA=USR3(0)
1041 REM check condor sopra 1up
1042 A=INT(PEEK(&HD0C6)/8):B=INT(X/8):IF PEEK(&HD017)=1 THEN IF A=B THEN POKE&HD015,1
1043 Q1=VPEEK(6144+INT((Y+16)/8)*32+((X+8)/8)):IF Q1=193 OR Q1=195 THENGOSUB1390:REM MORTE CON UOVO
1050 GOTO920
1060 REM CADUTA 1UP
1070 K=0:SP=22:PUTSPRITE6,(X,Y),6,SP:PUTSPRITE7,(X,Y),10,SP+1:PUTSPRITE8,(X,Y),15,SP+2:Y=Y+4:FORI=0TO5:NEXTI
1080 IFVPEEK(6144+INT((Y+16)/8)*32+((X)/8))=128THENGOTO1380ELSEGOTO1070
1090 REM ANIMAZIONE 1UP
1100 GOSUB1160:IFUP=136THENPS=1:SP=4:Y=Y-8:GOSUB1200:RETURNELSEPS=0:RETURN
1110 IFPS=0THENSP=0:GOSUB1140:X=X+8:GOSUB1180:RETURNELSERETURN
1120 GOSUB1170:IFDW=136THENPS=1:SP=4:Y=Y+8:GOSUB1200:RETURNELSEPS=0:RETURN
1130 IFPS=0THENSP=2:GOSUB1150:X=X-8:GOSUB1190:RETURNELSERETURN
1140 IFX=240THENRETURNELSEIFVPEEK(6144+INT(INT((Y)/8)*32+((X+16)/8)))=128THENX=X-8:RETURNELSERETURN
1150 IF VPEEK(6144+INT(INT((Y)/8)*32+((X-8)/8)))=128 THENX=X+8:RETURNELSERETURN
1160 UP=VPEEK(6144+INT(INT((Y)/8)*32+((X)/8))):RETURN
1170 DW=VPEEK(6144+INT(INT((Y+16)/8)*32+((X)/8))):RETURN
1180 IFSA=1THENSP=6:RETURNELSERETURN
1190 IFSA=1THENSP=8:RETURNELSERETURN
1200 IFSA=1THENSP=10:RETURNELSERETURN
1210 REM METTE SACCO SULLA CARIOLA
1211 A=USR3(0)
1220 IFSA=1ANDX=XA+24ANDY=YBTHENSA=0:SP=0:SC=SC+5:LOCATE0,1:PRINTSC*100:LOCATE25,1:POKE&HDA03,9:A=USR1(0):SS=SS+1
1225 IFSS=65THENFORI=0TO500:NEXTI:FORJ=0TO4:LOCATE 8,10+J:PRINTSTRING$(16,32):NEXTJ:LOCATE 8+2,12:PRINT " stage clear ":GOSUB1660:FORI=0TO1000:NEXTI:SS=0:LV=LV+1:ST=ST+1:GOTO690
1230 IF SA=1 THEN RETURN
1240 REM PRENDI SACCO
1250 Q2=VPEEK(6144+INT(INT((Y)/8)*32+((X)/8)))
1260 Q1=6144+INT(INT((Y)/8)*32+((X)/8))
1261 Q3=8192+PG*704+INT(Y/8)*32+INT(X/8)
1270 IFP=1THENP=0:PUTSPRITE10,,0,0:VPOKEQ3+35,162:VPOKEQ3+36,163:XA=X:YB=Y:POKE&HDA03,7:A=USR1(0):A=USR4(1+PG):A=USR3(0)
1280 IF Q2=144 OR Q2=146 THEN GOSUB 1341:SA=1:POKE&HDA03,10:A=USR1(0):IFSP=1THENGOSUB1180:RETURNELSEIFSP=7THENGOSUB1190:RETURN
1290 H2=VPEEK(6144+INT(INT((Y+8)/8)*32+((X+16)/8)))
1300 H1=6144+INT(INT((Y+8)/8)*32+((X+16)/8))
1301 H3=8192+PG*704+INT(INT((Y+8)/8)*32+((X+16)/8))
1310 IFH2=162THENVPOKEH2,32:VPOKEH2+1,32:VPOKE H3,32:VPOKE H3+1,32:A=USR(1+PG):A=USR3(0):P=1:XA=X:YB=Y:POKE&HDA03,4:A=USR1(0)
1320 REM PRESA ALLA MANIGLIA PER CARRELLO
1330 IFVPEEK(6144+INT((Y-8)/8)*32+((X+8)/8))=132THENSP=13:K=0:POKE&HDA03,6:A=USR1(0)
1340 RETURN
1341 REM clear one sacco
1342 VPOKE Q3,32:VPOKEQ3+1,32:VPOKEQ3+32,32:VPOKEQ3+33,32
1343 A=USR4(1+PG):A=USR3(0)
1344 B$=RIGHT$("0000"+HEX$(Q3),4)
1345 VPOKE V,VAL("&H"+LEFT$(B$,2))
1346 VPOKE V+1,VAL("&H"+RIGHT$(B$,2)):V=V+2
1347 RETURN
1348 REM clear vram 2048
1349 DEFUSR=&HD00F:A=USR(0):RETURN
1380 REM MORTE 1UP
1390 POKE&HDE05,14:A=USR5(0)
1400 SOUND7,56:SOUND12,25:SOUND11,0:SOUND10,0
1401 SOUND0,50:SOUND1,6:SOUND8,16
1402 SOUND2,50:SOUND3,7:SOUND9,16
1403 SOUND4,50:SOUND5,5:SOUND10,16
1404 FORI=0TO2:SOUND13,0:FORJ=0TO300:NEXTJ:NEXTI
1405 SOUND0,200:SOUND1,7:SOUND2,200:SOUND3,8:SOUND4,200:SOUND5,6
1406 SOUND12,35:SOUND13,0
1410 FORI=0TO2000:NEXTI
1420 LI=LI-1:IF LI<0 THEN1560
1430 LF=LI:IFLI>6THENLF=6:GOTO1450
1440 LOCATE 21+LF,0:PRINTCHR$(32)
1450 SP=1:X=48:Y=16:PUTSPRITE9,,0,0
1460 FORI=2TO5:PUTSPRITEI,,0,0:PUTSPRITE9,,0,0:NEXTI:TI=0:CR=0
1470 IFSA=1THENSS=SS+1:SA=0:PUTSPRITE5,,0,0
1480 POKE&HD017,0:POKE&HD016,0:A=USR3(1+PG):A=USR3(0):A=USR1(0):RETURN
1490 REM BONUS 1UP
1500 GOSUB 1680
1510 LI=LI+1
1520 LF=LI:IFLF>6THENLF=6
1530 LOCATE 20+LF,0:PRINTCHR$(134)
1540 RETURN
1550 REM GAME OVER
1560 LOCATE 10,10:PRINT" GAME OVER "
1570 GOSUB1580:GOSUB1680
1580 FORI=1 TO 3000:NEXTI
1590 GOTO 230
1600 REM PRINT SCENE
1610 X$=STR$(L):B$=RIGHT$(C$+RIGHT$(X$,LEN(RIGHT$(X$,LEN(X$)-1))),LEN(C$)):RETURN
1620 L=PG:L=L+1:C$="00":GOSUB1610:A$=B$:L=LV:GOSUB1610
1630 LOCATE 27,0:PRINT "SCENE":LOCATE 27,1:PRINT A$;"-";B$
1640 RETURN
1650 REM SFX SOUND
1660 POKE&HDE05,0:A=USR5(0)
1661 PLAY"V15T180O4S3M8000L4B-O6DCO5L8B-A-GA-CDS0M31000L1E-","V15T180S3M8000O2L4B-O3L8A-.R16A-.R16O2L4B-O4L8C.R16O3B-.O2L4E-GB-O3L8E-","V15T180S3M8000R4O4L8F.R16D.R16R4O3G.R16G.R16L2G."
1662 FORI=0TO2500:NEXTI
1665 FOR I=250 TO 50 STEP-5:FOR O=I TO I-50 STEP-8
1667 SOUND8,13:SOUND0,O:NEXT:NEXT:SOUND0,0
1668 RETURN
1670 REM
1680 RESTORE1710:GOSUB1690:IFI3=0THENPLAYZ(0),Z(1),Z(2):RETURNELSEFORI3=I3TO0STEP-1:READI0,I1,I2:PLAYZ(I0),Z(I1),Z(I2):NEXT:RETURN
1690 READI0,Z0,I1,I3:SOUND7,I0:PLAYZ0,Z0,Z0:FORI0=0TOI1:READZ(I0):NEXT:RETURN
1700 RETURN
1710 DATA&O70,t220s1m4096l8,2,0,O5EEEG+V12ES1BBBO6EV12O5BO6EV8O5BO6EV6O5BO6E,O5G+G+G+BV12G+S1O6D+D+D+G+V12D+G+V8D+G+V6D+G+,O4BBBO5EV12O4BS1O5G+G+G+BV12G+BV8G+BV6G+B
1720 REM CONDOR
1730 POKE&HD017,1:RETURN

